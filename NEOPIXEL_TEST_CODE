#include <Servo.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <Adafruit_NeoPixel.h>

// Servo 모터
const int SERVO = 10;
Servo servo;

// 초음파 센서(뚜껑 개폐)
const int trig_pin = 11;
const int echo_pin = 12;
// 초음파 센서(쓰레기양 측정)
const int trig_pin2 = 5; // 쓰레기양 측정 초음파 센서 핀
const int echo_pin2 = 9; // 쓰레기양 측정 초음파 센서 핀

// LED 스트립
const int LED_PIN = 8;
const int NUM_LEDS = 30;
Adafruit_NeoPixel strip = Adafruit_NeoPixel(NUM_LEDS, LED_PIN, NEO_GRB + NEO_KHZ800);

// 뚜껑 열리는 속도 설정
int cnt = 0;

LiquidCrystal_I2C lcd(0x27, 16, 2);

// 쓰레기통 높이
const int binHeight = 22;

void setup() {
  // LCD 셋업
  lcd.init();
  lcd.backlight();

  // 초음파 센서 셋업
  pinMode(trig_pin, OUTPUT);
  pinMode(echo_pin, INPUT);
  pinMode(trig_pin2, OUTPUT);
  pinMode(echo_pin2, INPUT);

  // Servo 모터 셋업
  servo.attach(SERVO);
  servo.write(0);
  delay(50);

  // LED 스트립 셋업
  strip.begin();
  strip.show(); // Initialize all pixels to 'off'

  Serial.begin(115200);
}

void loop() {
  // 뚜껑열리는 초음파 측정 시작
  digitalWrite(trig_pin, LOW);
  delayMicroseconds(2);
  digitalWrite(trig_pin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trig_pin, LOW);

  long duration = pulseIn(echo_pin, HIGH);
  long distance = ((duration * 0.0343) / 2);

  Serial.print(distance);
  Serial.println(" cm");
  delay(100);

  // 쓰레기양 초음파 측정 시작
  digitalWrite(trig_pin2, LOW);
  delayMicroseconds(2);
  digitalWrite(trig_pin2, HIGH);
  delayMicroseconds(10);
  digitalWrite(trig_pin2, LOW);

  long duration2 = pulseIn(echo_pin2, HIGH);
  long distance2 = ((duration2 * 0.0343) / 2);

  Serial.print(distance2);
  Serial.println(" cm");
  delay(100);

  // LCD에 표시할 메시지 결정
  String message;

  if (distance2 > binHeight * 0.9) {
    message = "EMPTY :)";
  } else if (distance2 > binHeight * 0.8) {
    message = "20%  O____";
  } else if (distance2 > binHeight * 0.6) {
    message = "40%  OO___";
  } else if (distance2 > binHeight * 0.4) {
    message = "60%  OOO__";
  } else if (distance2 > binHeight * 0.2) {
    message = "80%  OOOO_";
  } else {
    message = "FULL :(";
  }

  // LCD에 메시지 출력
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Bin Status:");
  lcd.setCursor(0, 1);
  lcd.print(message);

  delay(50);

  // Servo 모터 및 LED 스트립 제어
  if (distance <= 15) {
    if (cnt >= 10) {
      servo.write(140);
      for (int i = 0; i < NUM_LEDS; i++) {
        strip.setPixelColor(i, strip.Color(255, 0, 0)); // 빨간색으로 LED 켜기
      }
      strip.show();
    } else {
      cnt += 5;
    }
  } else {
    if (cnt == 0) {
      servo.write(90);
      for (int i = 0; i < NUM_LEDS; i++) {
        strip.setPixelColor(i, strip.Color(0, 0, 0)); // LED 끄기
      }
      strip.show();
    } else {
      cnt--;
    }
  }
  delay(100);
}
